#!/usr/bin/env fish

function get_volume
    pactl get-sink-volume @DEFAULT_SINK@ | grep -Po '[0-9]{1,3}(?=%)' | head -1
end

function get_volume_icon
    set icons 󰖁 󰕿 󰖀 󰕾
    echo $icons[(math ceil 1 + $argv[1] / 34)]
end

function show_volume_notification
    set vol (get_volume)
    set icon (get_volume_icon $vol)
    # notify-send -t 2000 -h string:x-dunst-stack-tag:volume -h int:value:$vol "$icon $vol%"
    notify-send -t 2000 -h string:x-dunst-stack-tag:volume "$icon $vol%"
end

function get_volume_muted
    pactl get-sink-mute @DEFAULT_SINK@ | cut -d' ' -f2
end

function get_mic_muted
    pactl get-source-mute @DEFAULT_SOURCE@ | cut -d' ' -f2
end

function clamp_volume
    if test (get_volume) -gt 100
        pactl set-sink-volume @DEFAULT_SINK@ 100%
    end
end

switch $argv[1]
    case up
        pactl set-sink-volume @DEFAULT_SINK@ +5%
        clamp_volume
        show_volume_notification
    case down
        pactl set-sink-volume @DEFAULT_SINK@ -5%
        show_volume_notification
    case mute
        pactl set-sink-mute @DEFAULT_SINK@ toggle
        switch (get_volume_muted)
            case yes
                notify-send -t 2000 -h string:x-dunst-stack-tag:volume "󰖁 Muted"
            case no
                show_volume_notification
        end
    case micmute
        pactl set-source-mute @DEFAULT_SOURCE@ toggle
        switch (get_mic_muted)
            case yes
                notify-send -t 2000 -h string:x-dunst-stack-tag:mic " Mic Muted"
            case no
                notify-send -t 2000 -h string:x-dunst-stack-tag:mic " Mic Unmuted"
        end
end
